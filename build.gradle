
plugins {
    id "org.sonarqube" version "2.6"
    id "jacoco"
}

allprojects {
    repositories {
        mavenCentral()
    }

    jacoco {
        toolVersion = '0.8.0'
    }
}

subprojects {
    group 'kr.itanoss'
    version '0.1-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'jacoco'

    sourceCompatibility = 1.8

    // ref: https://gist.github.com/aalmiray/e6f54aa4b3803be0bcac

    jacocoTestReport {
        additionalSourceDirs = files(sourceSets.main.allSource.srcDirs)
        sourceDirectories = files(sourceSets.main.allSource.srcDirs)
        classDirectories =  files(sourceSets.main.output)
        reports {
            html.enabled = true
            xml.enabled = true
            csv.enabled = false
        }
    }
}

task jacocoRootReport(type: JacocoReport) {
    dependsOn = subprojects.test
    additionalSourceDirs = files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories = files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories =  files(subprojects.sourceSets.main.output)
    executionData = files(subprojects.jacocoTestReport.executionData)
    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = false
    }
}
